apply plugin: 'com.android.application'

configurations {
    all {
        exclude module: 'com.springsource.org.apache.commons.logging'
    }
}

android {
    compileSdkVersion 26
    buildToolsVersion '27.0.3'

    defaultConfig {
        applicationId "org.ollide.rosandroid"
        // Android 4.0+ http://dannyroa.com/2013/10/17/why-its-time-to-support-only-android-4-0-and-above
        minSdkVersion 14
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        /* https://github.com/rosjava/android_core/issues/194 */
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
    sourceSets.main {
        jniLibs.srcDir 'src/main/libs'
        jni.srcDirs = [];
    }
}

dependencies {
    api project(':libraries:rosandroid-core')
    implementation project(':libraries:rosandroid-core')

    implementation 'org.ros.rosjava_core:rosjava_helpers:[0.3, 0.4)'
    // External libraries
    implementation 'com.hoho.android:usb-serial-for-android:[0.2, 0.3)'
    // Android libraries
    implementation 'com.android.support:support-v4:26.0.1'
}

tasks.withType(JavaCompile) {
    compileTask -> compileTask.dependsOn ndkBuild
}

task ndkBuild(type: Exec) {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    def ndkbuild = properties.getProperty('ndk.dir', null) + "/ndk-build"
    commandLine ndkbuild, '-C', file('src/main/jni').absolutePath
}
